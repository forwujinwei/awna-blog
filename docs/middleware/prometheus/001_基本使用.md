# 下载
https://prometheus.io/
# 安装
下载下来解压缩就好
# 启动

目录下的prometheus可以直接启动，然后通过localhost:9090端口访问，但是由于没有配置exporter来导入数据，暂时是没有数据的

# promql查询表达式
## base
即时矢量选择器

- =：匹配与标签相等的内容
- !=：不匹配与标签相等的内容
- =~: 根据正则表达式匹配与标签符合的内容
- !~：根据正则表达式不匹配与标签符合的内容

示例：
```
http_requests_total{environment=~"staging|testing|development",method!="GET"} #这将匹配method不等于GET,environment匹配到staging，testing或development的http_requests_total请求内容。
向量选择器必须指定一个名称或至少一个与空字符串不匹配的标签匹配器。以下表达式是非法的
{job=~"."} # Bad!
相反，这些表达式是有效的，因为它们都有一个与空标签值不匹配的选择器。
{job=~".+"} # Good!
{job=~".",method="get"} # Good!
```

范围矢量选择器

持续时间仅限于数字，接下来是以下单位之一：
- s - seconds
- m - minutes
- h - hours
- d - days
- w - weeks
- y - years

在此示例中，我们选择在过去5分钟内为度量标准名称为http_requests_total且标签设置为job=prometheus的所有时间序列记录的所有值：
```
http_requests_total{job="prometheus"}[5m]
```

偏移量修改器

偏移修改器允许更改查询中各个即时和范围向量的时间偏移。  

例如，以下表达式相对于当前查询5分钟前的http_requests_total值：
```
http_requests_total offset 5m
```

注意，偏移修改器需要立即跟随选择器，即以下内容是正确的：
```
sum(http_requests_total{method="GET"} offset 5m) // GOOD.
```
以下内容是不正确的：
```
sum(http_requests_total{method="GET"}) offset 5m // INVALID.
```
同样适用于范围向量。这将返回http_requests_total一周前的5分钟增长率：

```
rate(http_requests_total[5m] offset 1w)
```


## OPERATORS

二元运算符

### 算术二元运算符

Prometheus中存在以下二进制算术运算符：

- `+`(addition)
- `-`(subtraction)
- `*`(multiplication)
- `/` (division)
- `%` (modulo)
- `^` (power/exponentiation)

### 比较二元运算符

- `==` (equal)
- `!=` (not-equal)
- `>` (greater-than)
- `<` (less-than)
- `>=` (greater-or-equal)
- `<=` (less-or-equal)


### 逻辑/集二进制运算符
- and (intersection)
- or (union)
- unless (complement)

### 一对一矢量匹配

一对一从操作的每一侧找到唯一的条目对。在默认情况下，这是格式为vector1 vector2之后的操作。如果两个条目具有完全相同的标签集和相应的值，则它们匹配。 ignore关键字允许在匹配时忽略某些标签，而on关键字允许将所考虑的标签集减少到提供的列表：
ignoring()
on()
Example input:

```
method_code:http_errors:rate5m{method="get", code="500"} 24
method_code:http_errors:rate5m{method="get", code="404"} 30
method_code:http_errors:rate5m{method="put", code="501"} 3
method_code:http_errors:rate5m{method="post", code="500"} 6
method_code:http_errors:rate5m{method="post", code="404"} 21

method:http_requests:rate5m{method="get"} 600
method:http_requests:rate5m{method="del"} 34
method:http_requests:rate5m{method="post"} 120
```
Example query:

```
method_code:http_errors:rate5m{code="500"} / ignoring(code) method:http_requests:rate5m
```

这将返回一个结果向量，其中包含每个方法的状态代码为500的HTTP请求部分，在过去5分钟内测量。在不忽略（代码）的情况下，由于度量标准不共享同一组标签，因此不会匹配。方法put和del的条目没有匹配，并且不会显示在结果中：

```
{method="get"} 0.04 // 24 / 600
{method="post"} 0.05 // 6 / 120
```

### 多对一和一对多矢量匹配

多对一和一对多匹配指的是"一"侧的每个向量元素可以与"多"侧的多个元素匹配的情况。必须使用group_left或group_right修饰符明确请求，其中left/right确定哪个向量具有更高的基数。

```
ignoring() group_left()
ignoring() group_right()
on() group_left()
on() group_right()
```

Example query:

```
method_code:http_errors:rate5m / ignoring(code) group_left method:http_requests:rate5m
```
在这种情况下，左向量每个方法标签值包含多个条目。因此，我们使用group_left表明这一点。右侧的元素现在与多个元素匹配，左侧具有相同的方法标签：

```
{method="get", code="500"} 0.04 // 24 / 600
{method="get", code="404"} 0.05 // 30 / 600
{method="post", code="500"} 0.05 // 6 / 120
{method="post", code="404"} 0.175 // 21 / 120
```
多对一和一对多匹配是高级用例，应该仔细考虑。通常正确使用忽略（）可提供所需的结果。

### 聚合运算符

Prometheus支持以下内置聚合运算符，这些运算符可用于聚合单个即时向量的元素，从而生成具有聚合值的较少元素的新向量：
- sum (calculate sum over dimensions) #范围内求和
- min (select minimum over dimensions) #范围内求最小值
- max (select maximum over dimensions) #范围内求最大值
- avg (calculate the average over dimensions) #范围内求最大值
- stddev (calculate population standard deviation - over dimensions) #计算标准偏差
- stdvar (calculate population standard variance over dimensions) #计算标准方差
- count (count number of elements in the vector) #计算向量中的元素数量
- count_values (count number of elements with the same value) #计算向量中相同元素的数量
- bottomk (smallest k elements by sample value)#样本中最小的元素值
- topk (largest k elements by sample value)#样本中最大的元素值
- quantile (calculate φ-quantile (0 ≤ φ ≤ 1) over dimensions) #计算 0-1 之间的百分比数量的样本的最大值

这些运算符可以用于聚合所有标签维度，也可以通过包含without或by子句来保留不同的维度。
([parameter,] ) [without|by ()]

parameter仅用于count_values，quantile，topk和bottomk。without从结果向量中删除列出的标签，而所有其他标签都保留输出。

by相反并删除未在by子句中列出的标签，即使它们的标签值在向量的所有元素之间是相同的

例：如果http_requests_total具有按application，instance和group标签列出的时间序列，我们可以通过以下方式计算每个应用程序和组在所有实例上看到的HTTP请求总数：

```
sum(http_requests_total) without (instance)
```
等于：
```
sum(http_requests_total) by (application, group)
```
如果我们只对我们在所有应用程序中看到的HTTP请求总数感兴趣，我们可以简单地写：
```
sum(http_requests_total)
```
要计算运行每个构建版本的二进制文件的数量，我们可以编写：
count_values("version", build_version)
要在所有实例中获取5个最大的HTTP请求计数，我们可以编写：
```
topk(5, http_requests_total)
```
### FUNCTIONS

- abs(v instant-vector) #返回其绝对值
- absent() # 如果传递给它的向量具有该元素，则返回空向量;如果传递给它的向量没有元素，则返回传入的元素。

Example query:

```
nginx_server_connections
nginx_server_connections{endpoint="metrics",instance="192.168.43.5:9913",job="nginx-vts",namespace="dev",pod="nginx-vts-9fcd4d45b-sdqds",service="nginx-vts",status="accepted"} 89061
nginx_server_connections{endpoint="metrics",instance="192.168.43.5:9913",job="nginx-vts",namespace="dev",pod="nginx-vts-9fcd4d45b-sdqds",service="nginx-vts",status="handled"} 2
```

```
absent(nginx_server_connections{job="nginx-vts"}) => {}
absent(nginx_server_connections{job="nginx-vts123"}) => {job="nginx-vts123"}
```

- ceil(v instant-vector) #返回向量中所有样本值(向上取整数)
- round(v instant-vector, to_nearest=1 scalar) #返回向量中所有样本值的最接近的整数,to_nearest是可选的,默认为1,表示样本返回的是最接近1的整数倍的值, 可以指定任意的值(也可以是小数),表示样本返回的是最接近它的整数倍的值
- floor(v instant-vector) #返回向量中所有样本值(向下取整数)
- changes(v range-vector) #对于每个输入时间序列，返回其在时间范围内（v range-vector）更改的次数
- clamp_max(v instant-vector, max scalar) #限制v中所有元素的样本值，使其上限为max
- clamp_min(v instant-vector, min scalar) #限制v中所有元素的样本值，使其下限为min
- year(v=vector(time()) instant-vector) #返回UTC中给定时间的年份
- day_of_month(v=vector(time()) instant-vector) #返回UTC中给定时间的月中的某一天，返回值为1到31
- day_of_week(v=vector(time()) instant-vector) #返回UTC中给定时间的当周中的某一天，返回值为0到6
- days_in_month(v=vector(time()) instant-vector) #返回UTC中给定时间的一个月的天数，返回值28到31
- hour(v=vector(time()) instant-vector) #返回UTC中给定时间的当天中的某一小时，返回值为0到23
- minute(v=vector(time()) instant-vector) #返回UTC中给定时间的小时中的某分钟，返回值为0到59
- delta(v range-vector) #返回一个即时向量，它计算每个time series中的第一个值和最后一个值的差别
- deriv(v range-vector) #计算每个time series的每秒的导数(derivative)
- exp(v instant-vector) #计算v中所有元素的指数函数
- histogram_quantile(φ float, b instant-vector) #从buckets类型的向量中计算φ(0 ≤ φ ≤ 1)百分比的样本的最大值
- holt_winters(v range-vector, sf scalar, tf scalar) #根据范围向量中的范围产生一个平滑的值
- idelta(v range-vector) #计算最新的2个样本值之间的差别
- increase(v range-vector) #计算指定范围内的增长值, 它会在单调性发生变化时(如由于目标重启引起的计数器复位)自动中断
- irate(v range-vector) #计算每秒的平均增长值, 基于的是最新的2个数据点
- rate(v range-vector) #计算每秒的平均增长值
resets(v range-vector) #对于每个 time series , 它都返回一个 counter resets的次数
- sort(v instant-vector) #对向量按元素的值进行升序排序
- sort_desc(v instant-vector) #对向量按元素的值进行降序排序
- sqrt(v instant-vector) #返回v中所有向量的平方根
- time() #返回从1970-1-1起至今的秒数,UTC时间

# 安装grafana

# 导入监控模版

# 自定义监控


# 动态变更监控目标

上面 Prometheus 配置文件中定义的监控目标使用的是静态方式，改配置文件后需要重启。

如果服务变更了，或者增加服务了，经常重启 Prometheus 肯定不合适。

Prometheus 提供了动态加载的方式，把服务信息放到一个单独的文件中，Prometheus 配置中指定这个外部文件，内容变化后，Prometheus 就会自动重新加载。

服务信息配置文件例如：

```
[
    {
        "targets": [
            "192.168.31.6:8080"
        ],
        "labels": {
            "instance": "springboot2-A",
            "service": "springboot2-A-service"
        }
    }
]
```

Prometheus 配置文件中的写法：

```
...
  - job_name: 'springboot_app'
    scrape_interval: 5s
    metrics_path: '/actuator/prometheus'
    file_sd_configs:
    - files:
      - /home/*.json
      refresh_interval: 1m
```

## 常用函数

```
1. sum(..) by(instance) (increase()) 以instance分组然后相加
  
2. sum(..) without(instance) 去除instance，以剩下的标签分组然后相加
 
3. increase(..[1m])  一分钟内的值
  
4. rate(..[1m])  一分钟内的值除以60秒，如果是2m， 就除以120
 
5. irate(..[1m]) 和rate一样，不过取的是前面的值
  
6. topk(3,..) 取最大的3个值

7. ceil 对数值四舍五入

8. delta 计算一个范围向量v的第一个元素和最后一个元素之间的差值, delta(cpu_temp_celsius{host="zeus"}[2h])， 过去两小时的差值，前一个值减去最后一个值
```

## 函数使用样例

1. rate()
 rate() 平均比率。常和[1m]配合使用，一般统计对带有counter关键字的统计，代表一分钟内的某关键字的平均每秒增量。
```
例子1：计算所有节点1分钟内的网络接收总量的平均每秒比率
rate(node_network_receive_bytes_total[1m])


例子2：计算所有节点5分钟内的网络接收总量的平均每秒比率
这就比1分钟的比率的图形显得更平滑。
rate(node_network_receive_bytes_total[5m])
```

2. increase() 增量总和。取一段时间增量的总和

```
例子3：计算所有节点1分钟内的网络接收量在一分钟内的增量
increase(node_network_receive_bytes_total[1m])
```
3. sum() 求和。把关键字的所有对象加起来求和


```
例子4：计算所有节点1分钟内的网络接收量在一分钟内的总和的增量
sum(increase(node_network_receive_bytes_total[1m]))
```
4. topk() 取前几位的最高值。该函数长用于查看数据结果，而非是图形。
```
例子5：在所有网络接收总量中找到前三位的数据(一般不这么用)
topk(3,node_network_receive_bytes_total)
```


```
例子6：在30分钟内所有节点的网络接收总量比率中，排名前三的数据，实际中可能出图是不连续的，因为exporter抓数据的频率不一样，多台混合在一起就会造成时间错位，最终画图不连贯
topk(3,rate(node_network_receive_bytes_total[30m]))
```
5. count() 求数量  

一般用于模糊监控，比如共10台主机，其中2台的内存高于85%，这个时候我们不需要它报警，担当有5台以上才会报警，这个时候就可以使用count()函数来统计

## 实例
CPU使用率（值小了好）
----------------------------------------
空闲

```
sum(increase(node_cpu_seconds_total{mode="idle"}[1m])) by (instance)
```
总cpu
```
sum(increase(node_cpu_seconds_total[1m])) by (instance)
```
空闲率

```
(sum(increase(node_cpu_seconds_total{mode="idle"}[1m])) by (instance) / sum(increase(node_cpu_seconds_total[1m])) by (instance)) * 100
```
使用率

```
(1-(sum(increase(node_cpu_seconds_total{mode="idle"}[1m])) by (instance) / sum(increase(node_cpu_seconds_total[1m])) by (instance))) * 100
```



CPU IO等待率（值小了好）
----------------------------------------

```
(sum(increase(node_cpu_seconds_total{mode="iowait"}[1m])) by (instance) / sum(increase(node_cpu_seconds_total[1m])) by (instance)) * 100
```

内存使用率
----------------------------------------

```
(1-((node_memory_Buffers_bytes + node_memory_Cached_bytes + node_memory_MemFree_bytes) / node_memory_MemTotal_bytes)) * 100
```
文件系统（值小了好）
----------------------------------------

```
(1-(node_filesystem_free_bytes{mountpoint="/"} / node_filesystem_size_bytes{mountpoint="/"})) * 100
```

磁盘io（值小了好，得出的结果是MB）
----------------------------------------

```
((rate(node_disk_read_bytes_total[1m]) + rate(node_disk_written_bytes_total[1m])) / 1024 /1024) > 0
```

文件描述符（值小了好，返回百分比）
linux操作系统通常对每个进程能打开的文件数量有限制。
linux系统默认的最大文件描述符为1024。返回百分比，值小了好
-----------------------------------------

```
(node_filefd_allocated / node_filefd_maximum) * 100
```

